`timescale 1ns / 1ps
// 通用spi屏幕控制模块，用户只需要提供显示数据和坐标即可控制spi屏幕显示
module spi_screen_top(
    input                               sys_clk                    ,
    input                               sys_rst_n                  ,

    //用户信号
    output                              flush_data_update_o        ,//更新当前显示数据使能
    input              [  15: 0]        flush_data_i               ,//当前显示数据输入
    output             [  15: 0]        flush_addr_width_o         ,//当前刷新的x坐标
    output             [  15: 0]        flush_addr_height_o        ,//当前刷新的y坐标
    output                              spi_screen_flush_fsync_o   ,//屏幕帧同步


     //spi tft screen   屏幕接口
    output                              lcd_spi_sclk               ,// 屏幕spi时钟接口
    output                              lcd_spi_mosi               ,// 屏幕spi数据接口
    output                              lcd_spi_cs                 ,// 屏幕spi使能接口
    output                              lcd_dc                     ,// 屏幕 数据/命令 接口
    output                              lcd_reset                  ,// 屏幕复位接口
    output                              lcd_blk                     // 屏幕背光接口
);
//屏幕尺寸
    parameter                           SCREEN_WIDTH              = 32'd240;
    parameter                           SCREEN_HEIGHT             = 32'd240;

//屏幕用户接口
    wire               [   7: 0]        spi_screen_flush_data      ;//屏幕显示数据
    wire                                spi_screen_flush_updte     ;//屏幕数据更新
    wire                                spi_screen_flush_fsync     ;//屏幕帧同步

//坐标计数器
    reg                [  15: 0]        width_cnt                  ;
    reg                [  15: 0]        height_cnt                 ;

//数据更新计数器
    reg                                 data_update_cnt            ;


//显示数据寄存器
    reg                [  15: 0]        flush_data_reg             ;
//更新使能寄存器
    reg                                 flush_updte_en;

    assign  spi_screen_flush_data     = flush_data_reg[15:8];//高位数据

    //发送完16bit图像数据时，更新显示数据使能信号
    assign  flush_data_update_o = (spi_screen_flush_updte == 1'b1 && data_update_cnt == 1'b0 && flush_updte_en == 1'b1) ? 1'b1 : 1'b0;

    //坐标计数器输出到用户接口
    assign  flush_addr_width_o        = width_cnt;
    assign  flush_addr_height_o       = height_cnt;
always@(posedge sys_clk or negedge sys_rst_n) begin
    if( sys_rst_n == 1'b0)
        flush_updte_en <= 'd0;
    else if( spi_screen_flush_fsync == 1'b1 )   //刷新模块产生一帧结束，清零
        flush_updte_en <= 'd0;
    else if( spi_screen_flush_updte == 1'b1)    //发送完8位数据后，flush_updte_en置位
        flush_updte_en <= 'd1;
    else
        flush_updte_en <= flush_updte_en;
end
always@(posedge sys_clk or negedge sys_rst_n) begin
    if( sys_rst_n == 1'b0)
        data_update_cnt <= 'd0;
    else if( spi_screen_flush_fsync == 1'b1 )                       //刷新模块产生一帧结束
        data_update_cnt <= 'd0;
    else if( spi_screen_flush_updte == 1'b1)                        //刷新模块通过spi发送完一个8bit数据，data_update_cnt加1
        data_update_cnt <= data_update_cnt + 1'b1;                  //发送高8位时data_update_cnt=1，发送低8位时data_update_cnt=0
    else                                                            //所以发送16bit数据时data_update_cnt=0
        data_update_cnt <= data_update_cnt;
end


always@(posedge sys_clk or negedge sys_rst_n) begin
    if( sys_rst_n == 1'b0 )
        width_cnt <= 'd0;
    else if( spi_screen_flush_fsync == 1'b1 )                       //一帧图像数据发送结束，宽度计数器清零
        width_cnt <= 'd0;
    else if( flush_data_update_o)//需要更新当前发送的16bit图像数据时
        if( width_cnt == (SCREEN_WIDTH-1))                     //宽度计数器达到最大值时清零
            width_cnt <= 'd0;
        else
            width_cnt <= width_cnt + 1'b1;                          //width_cnt每次加1
    else
        width_cnt <= width_cnt;
end


always@(posedge sys_clk or negedge sys_rst_n) begin
    if( sys_rst_n == 1'b0 )
        height_cnt <= 'd0;
    else if( spi_screen_flush_fsync == 1'b1)                        //一帧图像数据发送结束，高度计数器清零
        height_cnt <= 'd0;
    else if( width_cnt == (SCREEN_WIDTH-1) && flush_data_update_o)//图像数据发送到一行结束时
        if( height_cnt == (SCREEN_HEIGHT-1))                     //高度计数器达到最大值时清零
            height_cnt <= 'd0;
        else
            height_cnt <= height_cnt + 1'b1;                        //height_cnt每次加1
    else
        height_cnt <= height_cnt;
end



always@(posedge sys_clk or negedge sys_rst_n) begin
    if( sys_rst_n == 1'b0)
        flush_data_reg <= 'd0;
    else if( spi_screen_flush_updte == 1'b1)               //刷新模块请求一个图像数据
        if( data_update_cnt == 1'b0 )                      //data_update_cnt=0时，需要发送新数据，寄存器更新
            flush_data_reg <= flush_data_i;
        else
            flush_data_reg <= flush_data_reg << 8;        //data_update_cnt=1时，需要发送高8位，寄存器左移8位，将低8位移到高8位
    else
        flush_data_reg <= flush_data_reg;
end

spi_tft_screen_driver spi_tft_screen_driver_inst(
    .sys_clk                            (sys_clk                   ),
    .sys_rst_n                          (sys_rst_n                 ),


    //用户接口
    .spi_screen_flush_data_i            (spi_screen_flush_data     ),//屏幕显示数据
    .spi_screen_flush_updte_o           (spi_screen_flush_updte    ),//屏幕数据更新//在需要数据时tft屏幕会请求
    .spi_screen_flush_fsync_o           (spi_screen_flush_fsync    ),//屏幕帧同步

     //spi tft screen   屏幕接口
    .lcd_spi_sclk                       (lcd_spi_sclk              ),// 屏幕spi时钟接口
    .lcd_spi_mosi                       (lcd_spi_mosi              ),// 屏幕spi数据接口
    .lcd_spi_cs                         (lcd_spi_cs                ),// 屏幕spi使能接口
    .lcd_dc                             (lcd_dc                    ),// 屏幕 数据/命令 接口
    .lcd_reset                          (lcd_reset                 ),// 屏幕复位接口
    .lcd_blk                            (lcd_blk                   ) // 屏幕背光接口
);

// 帧同步信号传递
assign spi_screen_flush_fsync_o = spi_screen_flush_fsync;
endmodule